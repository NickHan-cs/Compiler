# grammar

## 1. Origin Grammar

```
<程序>::= [<常量说明>][<变量说明>]{<有返回值函数定义>|<无返回值函数定义>}<主函数>
	<有返回值函数定义>::=<声明头部>'('<参数表>')''{'<复合语句>'}'
		<声明头部>::=int<标识符> 
					| char<标识符>
	<无返回值函数定义>::=void<标识符>'('<参数表>')''{'<复合语句>'}'	
		<参数表>::=<类型标识符><标识符>{,<类型标识符><标识符>}|<空>
	<主函数>::=void main '(' ')' '{' <复合语句> '}'
		<复合语句>::=[<常量说明>][<变量说明>]<语句列>
			<语句列>::={<语句>}

<常量说明>::=const<常量定义>;{const<常量定义>;}
	<常量定义>::=int<标识符>=<整数>{,<标识符>=<整数>}
				| char<标识符>=<字符>{,<标识符>=<字符>}
<变量说明>::=<变量定义>;{<变量定义>;}
	<变量定义>::=<变量定义无初始化> | <变量定义及初始化>
		<变量定义无初始化>::=<类型标识符>
						  (<标识符>
						   | <标识符>'['<无符号整数>']'
						   | <标识符>'['<无符号整数>']''['<无符号整数>']')
						  {,(<标识符>
							 | <标识符>'['<无符号整数>']'
							 | <标识符>'['<无符号整数>']''['<无符号整数>']' )}
		//变量包括简单变量、一维、二维数组，<无符号整数>表示数组各维元素的个数，其值需大于0，数组元素按行优先存储
		//变量没有初始化的情况下没有初值
		<变量定义及初始化>::=<类型标识符><标识符>=<常量>
						  | <类型标识符><标识符>'['<无符号整数>']'='{'<常量>{,<常量>}'}'
						  | <类型标识符><标识符>'['<无符号整数>']''['<无符号整数>']'='{''{'<常量>{,<常量>}'}'{, 								 '{'<常量>{,<常量>}'}'}'}'
		//简单变量、一维、二维数组均可在声明的时候赋初值，＜无符号整数＞表示数组各维元素的个数，其值需大于0，数组元素按行优先存		   储，＜常量＞的类型应与＜类型标识符＞完全一致，否则报错；每维初值的个数与该维元素个数一致，否则报错，无缺省值；

<语句>::=<循环语句>|<条件语句>|<有返回值函数调用语句>;|<无返回值函数调用语句>;|<赋值语句>;|<读语句>;|<写语句>;|<情况语句>|<空>;|<返回语句>;|'{'<语句列>'}'
	<循环语句>::=while '('<条件>')'<语句>
			  | for'('<标识符>=<表达式>;<条件>;<标识符>=<标识符>(+|-)<步长>')'<语句>
		<步长>::=<无符号整数>
	//for语句先进行条件判断，符合条件再进入循环体
	
	<条件语句>::=if'('<条件>')'<语句>[else<语句>]
		<条件>::=<表达式><关系运算符><表达式>
			<关系运算符>::= < | <= | > | >= | != | ==
	
	<有返回值函数调用语句>::=<标识符>'('<值参数表>')'
	
	<无返回值函数调用语句>::=<标识符>'('<值参数表>')'
	//函数调用时，只能调用在之前已经定义过的函数，对是否有返回值的函数都是如此
		<值参数表>::=<表达式>{,<表达式>}|<空>
		//实参的表达式不能是数组名，可以是数组元素
		//实参的计算顺序,要求生成的目标码运行结果与Clang8.0.0 编译器运行的结果一致
	
	<赋值语句>::=<标识符>=<表达式>
				| <标识符>'['<表达式>']'=<表达式>
				| <标识符>'['<表达式>']''['<表达式>']'=<表达式>
	//＜标识符＞＝＜表达式＞中的<标识符>不能为常量名和数组名
	
	<读语句>::=scanf'('<标识符>')'
	//从标准输入获取<标识符>的值，该标识符不能是常量名和数组名
	//生成的PCODE或MIPS汇编在运行时，对每一个scanf语句，无论标识符的类型是char还是int，末尾均需回车；在testin.txt文件中的输	  入数据也是每项在一行
	//生成MIPS汇编时按照syscall指令的用法使用
	
	<写语句>::=printf'('<字符串>,<表达式>')'
			  | printf'('<字符串>')'
			  | printf'('<表达式>')'
	//printf '(' ＜字符串＞,＜表达式＞ ')'输出时，先输出字符串的内容，再输出表达式的值，两者之间无空格
	//表达式为字符型时，输出字符；为整型时输出整数
	//＜字符串＞原样输出（不存在转义）
	//每个printf语句的内容输出到一行，按结尾有换行符\n处理
	
	<情况语句>::=switch '('<表达式>')' '{'<情况表><缺省>'}'
		<情况表>::=<情况子语句>{<情况子语句>}
			<情况子语句>::=case <常量>: <语句>
		<缺省>::=default: <语句>
	//情况语句中，switch后面的表达式和case后面的常量只允许出现int和char类型；每个情况子语句执行完毕后，不继续执行后面的情况子		语句
	
	<返回语句>::=return['('<表达式>')']
	//无返回值的函数中可以没有return语句，也可以有形如return;的语句
	//有返回值的函数只要出现一条带返回值的return语句（表达式带小括号）即可，不用检查每个分支是否有带返回值的return语句


<表达式>::=[+|-]<项>{<加法运算符><项>}
//[+|-]只作用于第一个<项>
	<项>::=<因子>{<乘法运算符><因子>}
		<因子>::=<标识符> 
				| <标识符>'['<表达式>']' 
				| <标识符>'['<表达式>']''['<表达式>']'
				| '('<表达式>')'
				| <整数>
				| <字符>
				| <有返回值函数调用语句>
		//char 类型的变量或常量，用字符的ASCII 码对应的整数参加运算
		//＜标识符＞'['＜表达式＞']'和＜标识符＞'['＜表达式＞']''['＜表达式＞']'中的＜表达式＞只能是整型，下标从0开始
		//单个＜标识符＞不包括数组名，即数组不能整体参加运算，数组元素可以参加运算

<类型标识符>::=int | char
<标识符>::=<字母>{<字母>|<数字>}
//标识符和保留字都不区分大小写，比如if和IF均为保留字，不允许出现与保留字相同的标识符

<字符串>::="{十进制编码为32,33,35-126的ASCII字符}"
//字符串中要求至少有一个字符
<常量>::=<整数> | <字符>
<字符>::='<加法运算符>' | '<乘法运算符>' | '<字母>' | '<数字>'
<加法运算符>::= + ｜ -
<乘法运算符>::= * ｜ /
<字母>::= _ | a | ... | z | A | ... | Z

<整数>::=[+|-]<无符号整数>
<无符号整数>::= <数字>{<数字>}
<数字>::=0 | 1 | ... | 9

另：关于类型和类型转换的约定：
1. 表达式类型为char型有以下三种情况：
	1）表达式由<标识符>、<标识符>'['<表达式>']和<标识符>'['<表达式>']''['<表达式>']'构成，且<标识符>的类型为char，即char类型的常量和变量、char类型的一维、二维数组元素。
	2）表达式仅由一个<字符>构成，即字符字面量。
	3）表达式仅由一个有返回值的函数调用构成，且该被调用的函数返回值为char型
	除此之外的所有情况，<表达式>的类型都是int
2. 只在表达式计算中有类型转换，字符型一旦参与运算则转换成整型，包括小括号括起来的字符型，也算参与了运算，例如(‘c’)的结果是整型。
3. 其他情况，例如赋值、函数传参、if/while条件语句中关系比较要求类型完全匹配，并且<条件>中的关系比较只能是整型之间比，不能是字符型。
```

## 2. 语法分析与超前扫描

<程序>递归分析子程序需要进行超前扫描

* const  ==>  [<常量说明>]
* <类型标识符> <标识符>  ('[' | '=' | ',' | ';')  ==>  [<变量说明>]
* (<类型标识符> | void) <标识符>  ==>  {<有返回值函数定义>|<无返回值函数定义>}
* <主函数>

**<程序>**::= [<常量说明>] [<变量说明>]{<有返回值函数定义>|<无返回值函数定义>}<主函数>

​		**<有返回值函数定义>**::=<声明头部>'('<参数表>')''{'<复合语句>'}'

​			**<声明头部>**::=int<标识符> | char<标识符>		

​		**<无返回值函数定义>**::=void<标识符>'('<参数表>')''{'<复合语句>'}'

​			**<参数表>**::=<类型标识符><标识符>{,<类型标识符><标识符>}|<空>

​	**<主函数>**::=void main '(' ')' '{' <复合语句> '}'

<复合语句>递归分析子程序

* const  ==>  [<常量说明>]
* <类型标识符>  ==>  [<变量说明>]
* <语句列>

​		**<复合语句>**::=[<常量说明>] [<变量说明>] <语句列>

​			**<语句列>**::={<语句>}

**<常量说明>**::=const<常量定义>;{const<常量定义>;}

​	**<常量定义>**::=int<标识符>=<整数>{,<标识符>=<整数>} 

​						| char<标识符>=<字符>{,<标识符>=<字符>}

**<变量说明>**::=<变量定义>;{<变量定义>;}

​	**<变量定义>**::=<变量定义无初始化> | <变量定义及初始化>

​		**<变量定义无初始化>**::=<类型标识符>
​						(<标识符>|<标识符>'['<无符号整数>']'|<标识符>'['<无符号整数>']''['<无符号整数>']'){,(<标识符>|<标识符>'['<无符号整数>']'|<标识符>'['<无符号整数>']''['<无符号整数>']' )}

​		**<变量定义及初始化>**::=<类型标识符><标识符>=<常量>

​			| <类型标识符><标识符>'['<无符号整数>']'='{'<常量>{,<常量>}'}'

​			| <类型标识符><标识符>'['<无符号整数>']''['<无符号整数>']'='{''{'<常量>{,<常量>}'}' {,'{'<常量>{,<常量>}'}'} '}'



<语句>递归分析子程序需要预读取

* while | for  ==>  <循环语句>
* if  ==>  <条件语句>
* <标识符> '('  ==>  <有返回值函数调用语句>; | <无返回值函数调用语句>;（要建立函数表，判断是否有返回值）
* <标识符> ('=' | '[')  ==>  <赋值语句>;
* scanf  ==>  <读语句>;
* printf  ==>  <写语句>;
* switch  ==>  <情况语句>
* ;  ==>  <空>;
* return  ==>  <返回语句>;
* '{'  ==>  '{'<语句列>'}'

**<语句>**::=<循环语句>|<条件语句>|<有返回值函数调用语句>;|<无返回值函数调用语句>;|<赋值语句>;|<读语句>;|<写语句>;|<情况语句>|<空>;|<返回语句>;|'{'<语句列>'}'

**<循环语句>**::=while '('<条件>')'<语句>
			  | for'('<标识符>=<表达式>;<条件>;<标识符>=<标识符>(+|-)<步长>')'<语句>

​	**<步长>**::=<无符号整数>



**<条件语句>**::=if'('<条件>')'<语句>[else<语句>]

​	**<条件>**::=<表达式><关系运算符><表达式>

​		<关系运算符>::= <[=] | >[=] | != | ==



**<有返回值函数调用语句>**::=<标识符>'('<值参数表>')'

**<无返回值函数调用语句>**::=<标识符>'('<值参数表>')'

​	**<值参数表>**::=<表达式>{,<表达式>}|<空>



**<赋值语句>**::=<标识符>=<表达式>

​				     | <标识符>'['<表达式>']'=<表达式>

​				     | <标识符>'['<表达式>']''['<表达式>']'=<表达式>



**<读语句>**::=scanf'('<标识符>')'

**<写语句>**::=printf'('<字符串>,<表达式>')'

​			     | printf'('<字符串>')'

​			     | printf'('<表达式>')'



**<情况语句>**::=switch '('<表达式>')' '{'<情况表><缺省>'}'

​		**<情况表>**::=<情况子语句>{<情况子语句>}

​			**<情况子语句>**::=case <常量>: <语句>

​		**<缺省>**::=default: <语句>



**<返回语句>**::=return['('<表达式>')']



**<表达式>**::=[+|-]<项>{<加法运算符><项>}

​	**<项>**::=<因子>{<乘法运算符><因子>}

​		**<因子>**::=<标识符> 

​				| <标识符>'['<表达式>']' 

​				| <标识符>'['<表达式>']''['<表达式>']'

​				| '('<表达式>')'

​				| <整数>

​				| <字符>

​				| <有返回值函数调用语句>



<类型标识符>::=int | char

<标识符>::=<字母>{<字母>|<数字>}



**<字符串>**::="{十进制编码为32,33,35-126的ASCII字符}"

**<常量>**::=<整数> | <字符>

<字符>::='<加法运算符>' | '<乘法运算符>' | '<字母>' | '<数字>'

<加法运算符>::= + ｜ -

<乘法运算符>::= * ｜ /

<字母>::= _ | a | ... | z | A | ... | Z





**<整数>**::=[+|-]<无符号整数>

**<无符号整数>**::= <数字>{<数字>}

<数字>::=0 | 1 | ... | 9



| 非终结符号             | 分析程序名          |
| ---------------------- | ------------------- |
| <程序>                 | program             |
| <有返回值函数定义>     | func_def_with_ret   |
| <声明头部>             | declaration_head    |
| <无返回值函数定义>     | func_def_no_ret     |
| <参数表>               | para_tlb            |
| <主函数>               | main_func           |
| <复合语句>             | compound_statements |
| <语句列>               | statement_list      |
| <常量说明>             | const_declaration   |
| <常量定义>             | const_def           |
| <变量说明>             | var_declaration     |
| <变量定义>             | var_def             |
| <变量定义无初始化>     | var_def_no_init     |
| <变量定义及初始化>     | var_def_with_init   |
| <语句>                 | statement           |
| <循环语句>             | loop_statement      |
| <步长>                 | step                |
| <条件语句>             | condition_statement |
| <条件>                 | condition           |
| <关系运算符>           | relation_op         |
| <有返回值函数调用语句> | func_call_with_ret  |
| <无返回值函数调用语句> | func_call_no_ret    |
| <值参数表>             | value_para_tlb      |
| <赋值语句>             | assign_statement    |
| <读语句>               | input_statement     |
| <写语句>               | output_statement    |
| <情况语句>             | case_statement      |
| <情况表>               | case_tlb            |
| <情况子语句>           | case_substatement   |
| <缺省>                 | default_statement   |
| <返回语句>             | ret_statement       |
| <表达式>               | expr                |
| <项>                   | term                |
| <因子>                 | factor              |
| <类型标识符>           | type_ident          |
| <标识符>               | ident               |
| <字符串>               | string_sym          |
| <常量>                 | const_sym           |
| <字符>                 | character           |
| <加法运算符>           | add_op              |
| <乘法运算符>           | multi_op            |
| <字母>                 | letter              |
| <整数>                 | integer             |
| <无符号整数>           | unsigned_integer    |
| <数字>                 | number              |

